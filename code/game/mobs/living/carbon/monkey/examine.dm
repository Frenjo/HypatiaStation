/mob/living/carbon/monkey/get_examine_header()
	. = list()
	. += SPAN_INFO_B("*---------*")
	. += SPAN_INFO("This is \icon[src] \a <em>[src]</em>!")
	if(desc)
		. += SPAN_INFO(desc)

/mob/living/carbon/monkey/get_examine_text()
	. = ..()
	if(isnotnull(wear_mask))
		. += SPAN_INFO("It has \icon[wear_mask] \a [wear_mask] on its head.")
	if(isnotnull(back))
		. += SPAN_INFO("It has \icon[back] \a [back] on its back.")
	if(isnotnull(l_hand))
		. += SPAN_INFO("It has \icon[l_hand] \a [l_hand] in its left hand.")
	if(isnotnull(r_hand))
		. += SPAN_INFO("It has \icon[r_hand] \a [r_hand] in its right hand.")
	if(isnotnull(handcuffed))
		. += SPAN_WARNING("It is \icon[handcuffed] handcuffed!")

	if(getBruteLoss())
		if(getBruteLoss() < 30)
			. += SPAN_WARNING("It has minor bruising.")
		else
			. += SPAN_DANGER("It has severe bruising!")
	if(getFireLoss())
		if(getFireLoss() < 30)
			. += SPAN_WARNING("It has minor burns.")
		else
			. += SPAN_DANGER("It has severe burns!")

	if(stat == DEAD)
		. += SPAN("deadsay", "It is limp and unresponsive, with no signs of life.")
	else if(stat == UNCONSCIOUS)
		. += SPAN_WARNING("It isn't responding to anything around it; it seems to be asleep.")

	if(digitalcamo)
		. += SPAN_INFO("It is repulsively uncanny!")

	. += SPAN_INFO_B("*---------*")