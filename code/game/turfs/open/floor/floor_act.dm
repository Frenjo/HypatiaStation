/turf/open/floor/ex_act(severity)
	//set src in oview(1)
	switch(severity)
		if(1.0)
			ChangeTurf(get_base_turf_by_area(src))
		if(2.0)
			switch(pick(1, 2;75, 3))
				if(1)
					ReplaceWithLattice()
					if(prob(33))
						new /obj/item/stack/sheet/steel(src)
				if(2)
					ChangeTurf(get_base_turf_by_area(src))
				if(3)
					if(prob(80))
						break_tile_to_plating()
					else
						break_tile()
					hotspot_expose(1000, CELL_VOLUME)
					if(prob(33))
						new /obj/item/stack/sheet/steel(src)
		if(3.0)
			if(prob(50))
				break_tile()
				hotspot_expose(1000, CELL_VOLUME)

/turf/open/floor/fire_act(datum/gas_mixture/air, exposed_temperature, exposed_volume)
	if(!burnt && prob(5))
		burn_tile()
	else if(prob(1))
		make_plating() // Plating can't be made into plating so this is probably fine.
		burn_tile()

/turf/open/floor/adjacent_fire_act(turf/open/floor/adj_turf, datum/gas_mixture/adj_air, adj_temp, adj_volume)
	var/dir_to = get_dir(src, adj_turf)

	for(var/obj/structure/window/W in src)
		if(W.dir == dir_to || W.is_fulltile()) //Same direction or diagonal (full tile)
			W.fire_act(adj_air, adj_temp, adj_volume)

/turf/open/floor/blob_act()
	return